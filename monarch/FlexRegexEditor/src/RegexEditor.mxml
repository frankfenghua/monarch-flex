<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns:ns1="http://www.adobe.com/2006/air"
	addedToStage="init(event)" viewSourceURL="srcview/index.html">
    <mx:Script>
        <![CDATA[
        import flash.html.HTMLLoader;
        import flash.net.URLRequest;  
		import mx.core.UIComponent;
        
        private var html:HTMLLoader;
        private var urlLoader:URLLoader;
        private var pageContents:String = "";
        private var currentMatches:Array;
		
		private function init(event:Event):void
		{
			html = new HTMLLoader();				
			html.addEventListener(Event.COMPLETE, onHTMLLoadComplete);
			
			// Create UIComponent to add html object to  
			var myComponent:UIComponent  = new UIComponent();  
			myComponent.addChild(html);  
  			
  			// now add component to stage  
			htmlBox.addChild(myComponent);  
		}
			
		private function onHTMLLoadComplete(event:Event):void {
			html.width = htmlBox.width;
    		html.height = 2880;
    		
    		var usrRegex:RegExp = new RegExp(regexString.text, "");
    		
    		// Reinitialize match label and match text
    		matchesLabel.text = "No captured text";
    		matchesText.text = "";
    		
    		// Begin highlighting matches from root node
    		trace(html);
    		highlightMatchesRec(html.window.document.documentElement, usrRegex);
		}
		

		private function highlightMatches(elemList:Object, regex:RegExp):void {
			// Highlight all elements in the element list that match the regular expression
			trace(elemList.length);
			for(var i:Number = 0; i < elemList.length; i++) {
				var match:Object = regex.exec(elemList[i].outerHTML);
				if(match != null) {
					if(match.length >= 2) {
						if(matchesLabel.text.charAt(0) == "N") {
							matchesLabel.text = "Captured Text"; 
						}
						matchesText.text += ("\n\n* " + match[1]);
					}
					elemList[i].style.background = "yellow";
					trace("Highlighting "+elemList[i].outerHTML.slice(0,10)+" . . . ");
				}
			}
		}
		
		private function highlightMatchesRec(elem:Object, regex:RegExp):void {	
			trace("Element type = " + elem.tagName);
			// trace("Element outerHTML = " + elem.outerHTML);
			// If there is no match within this node, return
			if(elem.outerHTML.search(regex) == -1) {
				return;
			}
			// There is a match so recurse and highlight matches
			else {
				var children:Object = elem.getElementsByTagName("*");
								
				var childHasMatch:Boolean = false;
				for(var i:Number = 0; i < children.length; i++) {
					if(children[i].outerHTML.search(regex) != -1) {
						childHasMatch = true;
						highlightMatchesRec(children[i], regex);
						return; // find only the first match
					}
				}
				
				// If there are no children which match the regex, run the tag-name-based
				// algorithm on nodes having the same tag name as this element
				if(childHasMatch == false) {
					trace("Tag Name = "+elem.tagName);
					highlightMatches(html.window.document.getElementsByTagName(elem.tagName), regex);
					// elem.style.background = "yellow";
				} 
			}
		}
		
		private function loadPage(event:Event):void {
			html.load(new URLRequest(searchTerms.text));
		}
		
		]]>
    </mx:Script>
    <mx:VBox height="51" horizontalAlign="left" width="1044">
    	<mx:HBox>
    		<mx:Label text="Website" />
    		<mx:TextInput id="searchTerms" text="http://reddit.com" />
    	</mx:HBox>
    	<mx:HBox>
    		<mx:Label text="Regular Expression" />
    		<mx:TextInput id="regexString"/>
    		<mx:Button label="Highlight Matches"
    			click="loadPage(event)"/>
    	</mx:HBox>
    </mx:VBox>
    <mx:HBox id="hBox1" horizontalAlign="left" verticalAlign="top" width="1042" height="100%">
        <mx:VBox height="100%" width="811" id="htmlBox">
        </mx:VBox>
        <mx:VBox height="100%" width="214" id="matchingTextBox">
            <mx:Label id="matchesLabel" text="" fontSize="10" fontWeight="bold" color="#0C5463"/>
            <mx:Text width="213" height="100%" id="matchesText" text="" color="#0C5463"/>
        </mx:VBox>
    </mx:HBox>
</mx:WindowedApplication>